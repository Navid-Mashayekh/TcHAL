<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.6">
  <POU Name="ring_buffer_CoE_read" Id="{787df9b3-d5d9-0373-0909-494e9e4ae403}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK ring_buffer_CoE_read
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR
    buffer : ARRAY[0..BUFFER_UPPER_BOUND] OF I_CoE_property;
    
    write_index: UDINT;
    read_index: UDINT;

	is_full_flag: BOOL;
END_VAR
VAR_IN_OUT
	// buffer : ARRAY[*] OF LWORD;
END_VAR
VAR CONSTANT 
    BUFFER_UPPER_BOUND : UINT:=50;
	BUFFER_CAPACITY : UINT:=BUFFER_UPPER_BOUND + 1;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[


]]></ST>
    </Implementation>
    <Property Name="capacity" Id="{7ae50e25-40e2-0503-1ab4-4b79d0c5f3e2}">
      <Declaration><![CDATA[PROPERTY capacity : uint]]></Declaration>
      <Get Name="Get" Id="{ba5e55e1-0f92-0c4a-19a1-1be990d4d1b3}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[capacity:= THIS^.buffer_capacity;
]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="clear" Id="{5019093c-6cb0-0561-3057-ab50779e2341}">
      <Declaration><![CDATA[METHOD clear : BOOL
VAR
	i: UINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[
FOR i:=0 TO BUFFER_UPPER_BOUND DO
	buffer[i] :=0;  
END_FOR

write_index:=0;
read_index:=0;
is_full_flag:=FALSE;
clear:=TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="is_empty" Id="{0ea29feb-a54c-046e-01ae-b3be700c30d5}">
      <Declaration><![CDATA[METHOD is_empty : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[is_empty:= write_index = read_index AND NOT is_full_flag;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="is_full" Id="{c4aebdf0-da04-00e9-3e05-3fd65b01c482}">
      <Declaration><![CDATA[METHOD is_full : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[is_full:= is_full_flag;

]]></ST>
      </Implementation>
    </Method>
    <Property Name="level" Id="{3c762a5f-8830-0528-0741-8995c58c976b}">
      <Declaration><![CDATA[PROPERTY level : uint]]></Declaration>
      <Get Name="Get" Id="{2ad2ffa0-b3b8-0d6d-3f3a-8823985e0f4b}">
        <Declaration><![CDATA[VAR
	tmp: INT;
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[
tmp := UDINT_TO_INT(write_index) - UDINT_TO_INT(read_index);

IF tmp < 0 THEN
    level := INT_TO_UINT(tmp + UINT_TO_INT(buffer_capacity));
ELSIF tmp = 0 AND is_full_flag THEN
    level := buffer_capacity;
ELSE
    level := INT_TO_UINT(tmp);
END_IF
]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="read" Id="{68b8e4ad-318c-045c-074c-78584d810734}">
      <Declaration><![CDATA[METHOD read : BOOL
VAR_OUTPUT
	data	: I_CoE_property;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT THIS^.is_empty() THEN
	data:= buffer[read_index];

    read_index:= (read_index + 1) MOD buffer_capacity;
    is_full_flag:=FALSE;
    read:=TRUE;
ELSE
    read:= FALSE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="write" Id="{694ddfee-ef73-0f43-03f3-623e609342a3}">
      <Declaration><![CDATA[METHOD write : BOOL
VAR_INPUT
	data	: I_CoE_property;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT THIS^.is_full() THEN
    buffer[write_index]:= data;
    write_index:= (write_index + 1) MOD buffer_capacity;
    is_full_flag:= write_index = read_index;
    write:=TRUE;
ELSE
    write:= FALSE;
END_IF

]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>