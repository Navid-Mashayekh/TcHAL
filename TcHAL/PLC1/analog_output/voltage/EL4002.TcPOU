<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.6">
  <POU Name="EL4002" Id="{93059a53-9188-024d-0977-f3d161064ec1}" SpecialFunc="None">
    <Declaration><![CDATA[(**
This function block abstracts the Beckhoff EtherCAT terminal EL4002 (0…10 V, 12 Bit)
*)
FUNCTION_BLOCK EL4002 EXTENDS CoE_TerminalBase IMPLEMENTS I_terminal_execution
VAR_INPUT
END_VAR
VAR_OUTPUT
	AO_Voltage_channel_1 		: I_AO_Voltage :=_channel_1;
	AO_Voltage_channel_2 		: I_AO_Voltage :=_channel_2;
END_VAR
VAR
	WcState_WcState			AT%I*: BOOL;
	InfoData_State			AT%I*: UINT;
	InfoData_AdsAddr_netId  AT%I*: AMSNETID;
	InfoData_AdsAddr_port 	AT%I*: WORD;
	
	AO_Outputs_Channel_1_Analog_output         AT%Q*: INT;
	AO_Outputs_Channel_2_Analog_output         AT%Q*: INT;
	
	_channel_1 : AO_Voltage_Channel(adc_min:=ADC_MIN, adc_max:=ADC_MAX, physical_min:=PHYSICAL_MIN, physical_max:=PHYSICAL_MAX, channel_number:=1);
	_channel_2 : AO_Voltage_Channel(adc_min:=ADC_MIN, adc_max:=ADC_MAX, physical_min:=PHYSICAL_MIN, physical_max:=PHYSICAL_MAX, channel_number:=2);
END_VAR
VAR CONSTANT
	ADC_MIN        	: INT:=0;
	ADC_MAX        	: INT:=32767;
	PHYSICAL_MIN 	: INT:=0;
	PHYSICAL_MAX 	: INT:=10;
	
    HARDWARE_REVSION : UINT:=19;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF NOT SUPER^._init_done THEN 
	_terminal_address:=InfoData_AdsAddr_port;
ELSE
	_channel_1.wcState:= NOT  WcState_WcState;
	_channel_1.terminal_address := _terminal_address;
	_channel_2.wcState:= NOT  WcState_WcState;
	_channel_2.terminal_address := _terminal_address;
	
	_channel_1();
	_channel_2();

	AO_Outputs_Channel_1_Analog_output := _channel_1.terminal_value;
	AO_Outputs_Channel_2_Analog_output := _channel_2.terminal_value;
END_IF]]></ST>
    </Implementation>
    <Method Name="FB_init" Id="{3ac9404f-03a7-0841-33de-974a670a9093}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
	CoE_property_handler: I_CoE_property_handler;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^._CoE_property_handler	:=CoE_property_handler;
_channel_1.CoE_property_handler	:=CoE_property_handler;
_channel_2.CoE_property_handler	:=CoE_property_handler;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="run" Id="{762758c1-78fd-0fc8-1275-319a8abe4da1}">
      <Declaration><![CDATA[METHOD run : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^();
THIS^();
]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>